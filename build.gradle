buildscript {
    ext {
        springBootVersion = '2.2.6.RELEASE'
        springCloudReleaseTrain = 'Hoxton.SR8'
    }
    repositories {
        jcenter()
        mavenLocal()
        maven { url 'https://dl.bintray.com/uw-it-edm/gradle-plugins' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "edu.uw.concert:gradle-gitflow:0.3.0"
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.5.2"
        classpath("org.jfrog.buildinfo:build-info-extractor-gradle:4.15.2")
        classpath("nu.studer:gradle-credentials-plugin:1.0.7")
    }
}

plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'edu.uw.concert.gitflow'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

apply plugin: 'nu.studer.credentials'
apply plugin: "com.jfrog.artifactory"
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

ext {
    gwsClientVersion = "1.0.0"
}

repositories {
    jcenter()
    mavenLocal()
    maven { url "https://repo.spring.io/milestone" }
}

bootJar {
    baseName = 'profile-api'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudReleaseTrain"
    }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-hateoas")
    compile('org.springframework.boot:spring-boot-starter-cache')
    compile('io.micrometer:micrometer-registry-statsd')
    runtime("org.pcollections:pcollections:3.1.3") // required for micrometer, expected fix in micrometer v1.0.6 (upgrading spring may fix this)


    compile("org.springframework.cloud:spring-cloud-starter-aws")

    compile('com.github.ben-manes.caffeine:caffeine')

    compile("edu.uw.edm.gws:gws-client:$gwsClientVersion")
    compile("edu.uw.edm.gws:gws-client-spring-boot-starter:$gwsClientVersion")


    compile("org.projectlombok:lombok")

    testCompile("org.springframework.boot:spring-boot-starter-test")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            // Override the default artifact ID (or try to)
            artifactId = jar.baseName

            groupId = 'edu.uw.edm.profile'

            // Set the parent so that the pom we get is valid -- otherwise we don't have any version numbers
            // for the Spring dependencies
            pom.withXml {
                asNode().appendNode('parent')
                        .appendNode('groupId', 'org.springframework.boot').parent()
                        .appendNode('artifactId', 'spring-boot-starter-parent').parent()
                        .appendNode('version', "${project.springBootVersion}")

                asNode().appendNode('repositories').appendNode('repository')
                        .appendNode('id', 'spring-releases').parent()
                        .appendNode('url', 'https://repo.spring.io/libs-release')
            }
        }
    }
}


artifactory {
    contextUrl =  System.getenv('ARTIFACTORY_CONTEXT')   //The base Artifactory URL if not overridden by the publisher/resolver


    publish {
        repository {
            // The Artifactory repository key to publish to
            def inferredRepoKey = System.getenv('ARTIFACTORY_SNAPSHOT_REPO')

            if (!version.toString().contains("SNAPSHOT") && !version.toString().contains("dirty")) {
                inferredRepoKey = System.getenv('ARTIFACTORY_RELEASE_REPO')
            }

            repoKey = inferredRepoKey
            logger.lifecycle "Using $inferredRepoKey as publishing repo"

            // Username comes from ~/.gradle/gradle.properties
            def resolveUserName = project.getProperties().get('eaiw.artifactoryPublishUser')
            if (! resolveUserName?.trim()) {
                // try to get it from an environment variable
                resolveUserName = System.getenv("ARTIFACTORY_PUBLISH_USER")
            }

            username = resolveUserName

            // Password comes from ~/.gradle/gradle.encrypted.properties
            if (credentials?.propertyMissing(resolveUserName)) {
                password = credentials[resolveUserName]
            }else {
                // try to get it from an environment variable
                password  = System.getenv("ARTIFACTORY_PUBLISH_PASSWORD")
            }
        }

        defaults {
            publications ('mavenJava')
        }
    }

    resolve {
        repository {
            // The Artifactory repository key to resolve through
            repoKey = System.getenv('ARTIFACTORY_RESOLVE_REPO')

            // Username comes from ~/.gradle/gradle.properties
            def resolveUserName = project.getProperties().get('eaiw.artifactoryResolveUser')
            if (! resolveUserName?.trim()) {
                // try to get it from an environment variable
                resolveUserName = System.getenv("ARTIFACTORY_PUBLISH_USER")
            }

            username = resolveUserName

            // Password comes from ~/.gradle/gradle.encrypted.properties
            if (credentials?.propertyMissing(resolveUserName)) {
                password = credentials[resolveUserName]
            }else {
                // try to get it from an environment variable
                password  = System.getenv("ARTIFACTORY_PUBLISH_PASSWORD")
            }
        }
    }
}


// make sure we have an artifact to publish
artifactoryPublish.dependsOn jar


if (version.toString().contains("dirty")) {
    artifactoryPublish.skip = true // as documented
    artifactoryPublish.onlyIf { false } // NOT documented

}

artifactoryPublish.dependsOn bootJar

//code coverage
jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}